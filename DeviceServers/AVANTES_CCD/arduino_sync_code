#include <SPI.h>
#include <Ethernet.h>

// MAC address for shield
byte mac[] = {0xA8, 0x61, 0x0A, 0xAE, 0x84, 0xDA};
IPAddress ip(10, 20, 30, 47);
EthernetServer server(80); // Using port 80
int led = 9; // LED attached to pin 9
unsigned int width = 10; // in us
unsigned long btw_ttl = 100; // in ms
int my_ttl_on = 0;
int time_val;

void setup() {
  pinMode(led, OUTPUT); // Led set as an output
  Ethernet.begin(mac, ip);
  server.begin();
  Serial.begin(9600); // Start serial communication
  Serial.println("Server address:"); // Print server address
  // (Arduino shield)
  Serial.println(Ethernet.localIP());
  my_ttl_on = 0;
}

void ttl_generate(int on, unsigned int width, unsigned long btw_ttl){
  if (on == 1){
    digitalWrite(led, HIGH);
    delayMicroseconds(width);
    digitalWrite(led, LOW);
    delay(btw_ttl);
  }
}

void loop() {
  ttl_generate(my_ttl_on, width, btw_ttl);
  EthernetClient client = server.available();
  if (client) {
    boolean currentLineIsBlank = true;
    String buffer = "";
    while (client.connected()) {
      if (client.available()) {
        char c = client.read(); // Read from the Ethernet shield
        buffer += c; // Add character to string buffer
        // Client sent request, now waiting for response
        if (c == '\n' && currentLineIsBlank) {
          client.println("HTTP/1.1 200 OK"); // HTTP response
          client.println("Content-Type: text/html");
          client.println(); // HTML code
          client.print("<center><br><h1>Turn on TTL generation</h1><br><br><br><FORM>");
          client.print("<P> <INPUT type=\"submit\" name=\"status\" value=\"ON\">");
          client.print("<P> <INPUT type=\"submit\" name=\"status\" value=\"OFF\">");
          client.print("</FORM></center>");
          break;
        }
        if (c == '\n') {
          currentLineIsBlank = true;
          buffer = "";
        }
        else if (c == '\r') { // Command from webpage
          // Did the on button get pressed
          if (buffer.indexOf("GET /?status=ON") >= 0){
            Serial.println("ON Was pressed");
            my_ttl_on = 1;
          }
          // Did the off button get pressed
          if (buffer.indexOf("GET /?status=OFF") >= 0){
            Serial.println("OFF Was pressed");
            my_ttl_on = 0;
          }
        }
        else {
          currentLineIsBlank = false;
        }
      }
    }
    client.stop(); // End server

  }
}